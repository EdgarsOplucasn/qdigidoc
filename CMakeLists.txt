cmake_minimum_required( VERSION 2.8 )
cmake_policy(SET CMP0020 NEW)
project( qdigidoc )

set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake/modules")
set(CMAKE_FIND_ROOT_PATH ${CMAKE_OSX_SYSROOT})
set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(CMAKE_AUTOMOC ON)

include( GNUInstallDirs )
include( VersionInfo )

find_package( PKCS11 )
find_package( LibDigiDocpp REQUIRED )
include_directories( ${LIBDIGIDOCPP_INCLUDE_DIR} )

find_package( Qt5Widgets QUIET HINTS ${QTSDK} )
if( Qt5Widgets_FOUND )
	find_package( Qt5Network HINTS ${QTSDK} )
	find_package( Qt5PrintSupport HINTS ${QTSDK} )
	find_package( Qt5LinguistTools HINTS ${QTSDK} )
	add_definitions( -DQT_DISABLE_DEPRECATED_BEFORE=0x040800 )
else()
	find_package( Qt4 4.8.0 COMPONENTS QtCore QtGui QtNetwork REQUIRED )
	include( ${QT_USE_FILE} )
endif()

set_ex( CONFIG_URL "$ENV{CONFIG_URL}" "https://installer.id.ee/media/updater/config.json" CACHE STRING "Set Config URL" )
set_ex( BREAKPAD "$ENV{BREAKPAD}" "https://cr.eesti.ee/" CACHE STRING "URL for breakpad crash reporting, empty for disabled" )

if( WIN32 )
	add_subdirectory( extensions/EsteidShellExtension )
elseif( APPLE )
	add_subdirectory( extensions/DigiDocQL )
elseif( UNIX )
	option(ENABLE_KDE "Install KDE service menu (default: TRUE)" TRUE)
	option(ENABLE_NAUTILUS_EXTENSION "Build Nautilus extension (default: TRUE)" TRUE)
	if (ENABLE_KDE)
		add_subdirectory( extensions/kde )
	endif()
	if (ENABLE_NAUTILUS_EXTENSION)
		add_subdirectory( extensions/nautilus )
	endif()
endif()
add_subdirectory( common )
add_subdirectory( crypto )
add_subdirectory( client )
